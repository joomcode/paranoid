apply plugin: 'kotlin'
apply plugin: 'idea'
apply from: "$rootDir/pablo.gradle"

targetCompatibility = javaVersion
sourceCompatibility = javaVersion

compileKotlin {
  kotlinOptions {
    jvmTarget = javaVersion
  }
}

dependencies {
  compileOnly gradleApi()
  compileOnly "com.android.tools.build:gradle:$androidToolsVersion"
  compileOnly "com.android.tools.build:gradle-api:$androidToolsVersion"

  relocate project(':processor')

  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
}

final File generatedDir = new File(projectDir, "generated")
final File generatedJavaSourcesDir = new File(generatedDir, "main/java")

task generateBuildClass {
  inputs.property('version', version)
  outputs.dir generatedDir

  doLast {
    final File buildClassFile = new File(generatedJavaSourcesDir, "com/joom/paranoid/plugin/Build.java")
    buildClassFile.parentFile.mkdirs()
    buildClassFile.text = "" +
        "package com.joom.paranoid.plugin;\n" +
        "\n" +
        "public class Build {\n" +
        "    public static final String VERSION = \"$version\";\n" +
        "}\n"
  }
}

sourceSets {
  main {
    java.srcDirs += generatedJavaSourcesDir
  }
}

tasks.getByPath(JavaPlugin.COMPILE_JAVA_TASK_NAME).dependsOn(tasks.generateBuildClass)
tasks.named('compileKotlin', org.jetbrains.kotlin.gradle.tasks.KotlinCompilationTask.class) {
  it.dependsOn(tasks.generateBuildClass)
}

clean.doFirst {
  delete generatedDir
}

idea {
  module {
    generatedSourceDirs += generatedJavaSourcesDir
  }
}

pablo {
  shadow {
    relocate 'com.joom.paranoid'
  }
}
